AC_INIT(clutter/cluttermmconfig.h.in)

#########################################################################
#  Version and initialization
#########################################################################
CLUTTERMM_MAJOR_VERSION=0
CLUTTERMM_MINOR_VERSION=9
CLUTTERMM_MICRO_VERSION=0

#
# +1 : ? : +1  == new interface that does not break old one
# +1 : ? : 0   == new interface that breaks old one
#  ? : ? : 0   == no new interfaces, but breaks apps
#  ? :+1 : ?   == just some internal changes, nothing breaks but might work
#                 better
# CURRENT : REVISION : AGE
CLUTTERMM_SO_VERSION=2:0:0

CLUTTERMM_VERSION=$CLUTTERMM_MAJOR_VERSION.$CLUTTERMM_MINOR_VERSION.$CLUTTERMM_MICRO_VERSION
CLUTTERMM_RELEASE=$CLUTTERMM_MAJOR_VERSION.$CLUTTERMM_MINOR_VERSION
AC_DEFINE_UNQUOTED([CLUTTERMM_MAJOR_VERSION], $CLUTTERMM_MAJOR_VERSION, major version)
AC_DEFINE_UNQUOTED([CLUTTERMM_MINOR_VERSION], $CLUTTERMM_MINOR_VERSION, minor version)
AC_DEFINE_UNQUOTED([CLUTTERMM_MICRO_VERSION], $CLUTTERMM_MICRO_VERSION, micro version)
AC_DEFINE_UNQUOTED([CLUTTERMM_VERSION], "$CLUTTERMM_VERSION", cluttermm version string)
AC_SUBST(CLUTTERMM_VERSION)
AC_SUBST(CLUTTERMM_RELEASE)
AC_SUBST(CLUTTERMM_SO_VERSION)

AC_CONFIG_AUX_DIR(scripts)

dnl For automake.
VERSION=$CLUTTERMM_VERSION
PACKAGE=cluttermm

dnl Initialize automake stuff
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

dnl Specify a configuration file (no autoheader)
AM_CONFIG_HEADER(clutter/cluttermmconfig.h)
AM_MAINTAINER_MODE
AL_ACLOCAL_INCLUDE(scripts)


#########################################################################
#  Configure arguments
#########################################################################

#########################################################################
#  Environment Checks
#########################################################################
AC_PROG_CC
AC_PROG_CPP
AC_PROG_MAKE_SET
AC_CANONICAL_HOST
AM_DISABLE_STATIC
AM_PROG_LIBTOOL

AL_PROG_GNU_M4(AC_MSG_ERROR([dnl
SUN m4 does not work for building cluttermm.
Please install GNU m4.]))

AL_PROG_GNU_MAKE(AC_MSG_ERROR([dnl
SUN make does not work for building cluttermm.
Please install GNU make.]))

AL_PROG_PERL(AC_MSG_ERROR([Perl is required to build cluttermm.]))

AC_CHECK_PROGS(PERL, perl5 perl)

#########################################################################
#  Dependancy checks
#########################################################################

# Note: We depend on cairomm only because pangomm depends on it
# (but it was not in the pangomm .pc file until recently).
PKG_CHECK_MODULES(CLUTTERMM,
                  [clutter-0.9
                   glibmm-2.4 >= 2.14
                   pangomm-1.4 >= 2.12
                   cairomm-1.0])
AC_SUBST(CLUTTERMM_CFLAGS)
AC_SUBST(CLUTTERMM_LIBS)


# the API version of the library.  Libraries and pkg-config files will have this
# value appended to them, e.g. libclutter-0.8.so
API_VER=[0.9]
AC_SUBST(API_VER)

GMM_PROCDIR=`pkg-config --variable=gmmprocdir glibmm-2.4`
AC_SUBST(GMM_PROCDIR)
GMM_PROC=$GMM_PROCDIR/gmmproc
AC_SUBST(GMM_PROC)

#########################################################################
#  C++ checks
#########################################################################
AC_PROG_CXX
AC_LANG_CPLUSPLUS


# HACK:  Assign a dummy in order to prevent execution of autoheader by the
# maintainer-mode rules.  That would fail since we aren't using autoheader.
AUTOHEADER=':'


AC_OUTPUT([
  Makefile

  clutter/Makefile
  clutter/cluttermm-$API_VER.pc:clutter/cluttermm.pc.in
  clutter/src/Makefile
  clutter/cluttermm/Makefile
  clutter/cluttermm/private/Makefile

  tools/Makefile
    tools/extra_defs_gen/Makefile
    tools/m4/Makefile

  scripts/Makefile

  docs/Makefile
  docs/reference/Makefile
    docs/reference/Doxyfile

  examples/Makefile
  tests/Makefile
])
